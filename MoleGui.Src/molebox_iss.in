; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppId={{5348E0FD-C5CC-4CFA-9336-12D10C8977FD}}
AppName=__NAME
UninstallDisplayName=__NAME
SetupIconFile="__WORKDIR/../molebox.SRC/logo.ico"
UninstallDisplayIcon="{app}\molebox.exe"
AppVerName=__NAME __VERSION (__BUILD)
AppPublisher=DesaNova Ltda.
AppPublisherURL=www.molebox.com
AppSupportURL=www.molebox.com
AppUpdatesURL=www.molebox.com
DefaultDirName=__FOLDER_NAME
DefaultGroupName=__GROUP_NAME

OutputBaseFilename=__DISTRIB_NAME
;OutputBaseFilename=MoleBoxRelease
OutputDir=__SETUPDIR
SourceDir=__WORKDIR

LicenseFile=license4setup.txt
;InfoAfterFile=readme4setup.txt

AllowNoIcons=yes
ChangesAssociations=yes
ChangesEnvironment=yes

[Tasks]
Name: "desktopicon"; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"

[Files]
Source: "..\dlls\*.dll";  DestDir: "{app}"; Flags: ignoreversion
Source: "..\dlls\*.dll";  DestDir: "{app}\examples\dlls"; Flags: ignoreversion
Source: "molebox.exe";  DestDir: "{app}"; Flags: ignoreversion
Source: "mxbpack.exe";  DestDir: "{app}"; Flags: ignoreversion
Source: "mxbreg.exe";   DestDir: "{app}"; Flags: ignoreversion
;Source: "MANUAL.pdf";  DestDir: "{app}"; Flags: ignoreversion
;Source: "LICENSE.pdf"; DestDir: "{app}"; Flags: ignoreversion
Source: "license.txt";  DestDir: "{app}"; Flags: ignoreversion
Source: "examples\*";   DestDir: "{app}\examples"; Flags: ignoreversion recursesubdirs
Source: "data\*";       DestDir: "{app}\data"; Flags: ignoreversion recursesubdirs
Source: "..\Help\molebox.chm";   DestDir: "{app}"; Flags: ignoreversion
;NOTE: Don't use "Flags: ignoreversion" on any shared system files

[INI]
Filename: "{app}\moleboxSite.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.molebox.com/"
Filename: "{app}\moleboxInfo.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://info.molebox.com/"

[Icons]
Name: "{group}\MoleBox"; Filename: "{app}\molebox.exe"
Name: "{group}\MoleBox on the Web"; Filename: "{app}\moleboxSite.url"
Name: "{group}\MoleBox Info Site"; Filename: "{app}\moleboxInfo.url"
Name: "{group}\MoleBox Manual"; Filename: "{app}\molebox.chm"
Name: "{group}\MoleBox Examples"; Filename: "{app}\examples"
;Name: "{group}\Uninstall MoleBox"; Filename: "{uninstallexe}"
Name: __ICON; Filename: "{app}\molebox.exe"; Tasks: desktopicon
Name: "{userdesktop}\MoleBox Manual"; Filename: "{app}\molebox.chm"; Tasks: desktopicon

[Run]
Filename: "{app}\molebox.exe"; Description: "Launch MoleBox"; Flags: nowait postinstall skipifsilent runasoriginaluser

[UninstallDelete]
Type: files; Name: "{app}\moleboxInfo.url"
Type: files; Name: "{app}\moleboxSite.url"
Type: filesandordirs; Name: "{app}\examples"

[Registry]
Root: HKCU; Subkey: "Software\Classes\.mxb"; ValueType: string; ValueName: ""; ValueData: "MoleBox.Script"; Flags: uninsdeletevalue
Root: HKCU; Subkey: "Software\Classes\MoleBox.Script"; ValueType: string; ValueName: ""; ValueData: "Teggo MoleBox Script"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Classes\MoleBox.Script\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\molebox.EXE,0"
Root: HKCU; Subkey: "Software\Classes\MoleBox.Script\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\molebox.EXE"" ""%1"""
Root: HKCU; Subkey: "Software\Environment"; ValueType: string; ValueName: "MOLEBOXHOME"; ValueData: "{app}"

[Code]                                           
procedure DeinitializeUninstall();
var
ErrorCode: Integer;
begin
{*if MsgBox('Thank you for trying MoleBox.' #13#13 'Would you like to send us your feedback?' #13 '(this will open your web browser)', mbConfirmation, MB_YESNO) = idYes then*}
ShellExec('open', 'http://www.molebox.com/uninstall?os='+GetWindowsVersionString()+'&version=__VERSION.__BUILD', '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode);
end; 
